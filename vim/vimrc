" encoding setting
" encoding setting for vim system
set encoding=utf-8
" encoding setting for current buffer
set fileencoding=utf-8
" encoding setting for all buffer
set fileencodings=utf-8
" encoding setting for vim script
scriptencoding utf-8

"vimrcを再読込する際にautocmdが重複しないようにautocmdをすべて解除
augroup local
    autocmd!
augroup END

"neovim, windiws, unix系で分岐
if has('nvim')
    if exists("$XDG_CONFIG_HOME")
        let g:vimdir = expand($XDG_CONFIG_HOME . "/nvim/")
    else
        let g:vimdir = expand('~/.config/nvim/')
    endif
else
    if has('win32')
        let g:vimdir = expand('~/vimfiles/')
    else
        let g:vimdir = expand('~/.vim/')
    endif
endif

if exists('$VIM_TERMINAL')
    echo 'Warning!! vim is already opened!!'
endif

"local設定ファイル (1st)
let s:initrc = g:vimdir . 'rcdir/init.vim.mine'
if filereadable(s:initrc)
    execute "source " . s:initrc
endif

" basic setup (set, map系) 読み込み
let s:basicrc = g:vimdir . 'rcdir/vimrc_basic.vim'
if filereadable(s:basicrc)
    execute "source " . s:basicrc
else
    echohl Error
    echo "basic setup Run Command file can't be readed!!!"
    echo "end setting"
    echohl None
    finish
endif

""##########autocmd系 "{{{
autocmd local BufEnter * let b:no_match_paren = 1   " tentative? because of paren error (Highlight_matching_Pair()).

" remove tags information when open new tab.
if exists("*settagstack")
    autocmd local TabNew * call settagstack(winnr(), {'length':0, 'curidx':1, 'items':[]})
endif

" toml fileのfiletypeをtomlにする
autocmd local BufEnter *.toml set filetype=toml

"vimgrepした際に新規windowで開くようにする
autocmd local QuickFixCmdPost *grep* cwindow

"quick fix windowでc-tで新しいtabで開く
autocmd local Filetype qf nnoremap <buffer><silent> <c-t> <c-w><s-t><CR>:cclose<CR>

" .mine系ファイルのtype設定
autocmd local BufEnter *bashrc* set filetype=sh
autocmd local BufEnter *zshrc* set filetype=zsh

"autocmd local Filetype qf setlocal stl=%t%{exists('w:quickfix_title')?\ '\ '.w:quickfix_title\ :\ ''}\ %=%l/%L\ %P
"See $VIMRUNTIME/ftplugin/qf.vim to change quickfix window statusline
" }}}

""########## (簡単な)作成関数とか{{{
"ls in vim
command! LS 0tabnew .

""statusline setting {{{
let g:l_st_fileformat = {"":"", "unix":"unix,LF", "mac":"mac,CR", "dos":"dos,CRLF"}
let g:l_st_mode = {'n':'NORMAL',
            \'v':'VISUAL', "\<C-V>":'VISUAL-B', 'V':'VISUAL-L',
            \'i':'INSERT',
            \'R':'REPLACE',
            \'t':'TERMINAL',
            \'c':'COMMAND',
            \}
" mode
" ファイル名 修正フラグ 読込専用 ヘルプ preview_window
" 切り詰め位置 右端に表示
" filetype
" file format
" 今の行/全体の行-今の列 [%表示]
""" the variable to administer statusline
let g:l_statusline = [
            \" %f%m%{&readonly?'[RO]':''}%h%w ",
            \"%<%=",
            \"%#StatusLine_FT# %{&filetype} ",
            \"%#StatusLine_FF# %{g:l_st_fileformat[&fileformat]}:%{&fileencoding} ",
            \"%#StatusLine_LN# %l/%L-%v %#StatusLine#[%P]",
            \]

let g:l_statusline_off = ["%t%m%{&readonly?'[RO]':''}%h%w"]
""" control statusline showing
let g:l_st_width_level = [30, 50]
let g:l_st_showmode = 1
function! Set_statusline(cur_win, ...)
    if a:0 == 0
        let l:st_list1 = g:l_statusline
    else
        let l:st_list1 = a:1
    endif

    if winwidth(".") < g:l_st_width_level[0]
        let l:st_list2 = [l:st_list1[0]]
    elseif winwidth(".") < g:l_st_width_level[1]
        if len(st_list1) >= 3
            let l:st_list2 = [l:st_list1[0], l:st_list1[1], l:st_list1[-1]]
        else
            let l:st_list2 = [l:st_list1[0]]
        endif
    else
        let l:st_list2 = l:st_list1
    endif

    let l:st_str = ""
    for l:st in l:st_list2
        let l:st_str .= l:st
    endfor

    if (g:l_st_showmode != 0) && (a:cur_win != 0)
        let modec = mode()
        " mode string setting
        let st_mode = has_key(g:l_st_mode, modec) ? g:l_st_mode[modec] : modec
        " color setting
        let st_mode .= ' %#StatusLine#'
        if modec =~ 'i'
            let st_mode = '%#Mode_I#' . st_mode
        elseif modec =~ 'v' || modec == "\<C-V>"
            let st_mode = '%#Mode_V#' . st_mode
        elseif modec =~ 'r'
            let st_mode = '%#Mode_R#' . st_mode
        elseif modec =~ 't'
            let st_mode = '%#Mode_T#' . st_mode
        else
            let st_mode = '%#Mode_N#' . st_mode
        endif
        let l:st_str = st_mode . l:st_str
    endif

    return l:st_str
endfunction
set statusline=%!Set_statusline(1)
" g:l_st_showmode != 0 なら(statuslineに表示されるので) showmode をoffにする
autocmd local VimEnter * if g:l_st_showmode!=0 | set noshowmode | endif

"}}}

"ファイルが読み込めない事があるので、その時用にread onlyをつけてencodeし直して開く関数 "{{{

function! s:noeol_reenc()
    if &endofline == 0
        if input("reencode? 'y' or 'n'")=='y'
            set readonly
            e ++enc=utf-8
        endif
    endif
endfunction

"そしてファイルを開くたびに行うようにautocmd化
autocmd local BufRead * :call s:noeol_reenc()
" }}}

"tabの表示を快適に {{{
" https://qiita.com/wadako111/items/755e753677dd72d8036d
" Anywhere SID.
function! s:SID_PREFIX()
  return matchstr(expand('<sfile>'), '<SNR>\d\+_\zeSID_PREFIX$')
endfunction

" Set tabline.
function! s:get_title(tabnr)
    let bufnrs = tabpagebuflist(a:tabnr)
    let bufnr = bufnrs[tabpagewinnr(a:tabnr)-1]
    let mod = getbufvar(bufnr, '&modified') ? '[+]' : ''
    let title = fnamemodify(bufname(bufnr), ':t')
    if getbufvar(bufnr, '&filetype') == 'qf'
        let title = "QuickFix"
    endif
    let title = '[' . title . ']'
    let title = a:tabnr . ':' . title . mod
    return title
endfunction

function! s:get_cwd_path()
    let max_dir_len = &columns/5
    if has('win32')
        let sep = '\'
    else
        let sep = '/'
    endif
    let dirs = getcwd()
    let dirs = split(dirs, sep)
    let ret = '/'
    for d in dirs
        if d == dirs[-1]
            let ret .= d.sep
        else
            let ret .= d[0].sep
        endif
    endfor

    " ssh 先ではhostnameも表示
    if exists('$SSH_CLIENT')
        let hname = hostname()[:match(hostname(), '\.')-1] . ':'
    else
        let hname = ''
    endif

    " 長い場合は切る
    if len(ret)+len(hname) > max_dir_len
        let ret = '~~'.ret[-(max_dir_len-len(hname)-2):]
    endif

    return hname.ret
endfunction

function! s:my_tabline()
    let width = &columns
    let s = ''
    let cur_tab_no = tabpagenr()
    let header = '..'
    let footer = '..'
    let all_files = 0
    let all_wins = 0
    let is_edit = 0
    let l_opened_bn = []
    let tab_len = 16 " max(header + footer) = 12+2+2
    let tab_fin_l = 0
    let tab_fin_r = 0

    let cdir = ' @'.s:get_cwd_path()
    let tab_len += len(cdir)

    for i in range(1, tabpagenr('$'))
        " left side of current tab page (include current tab page).
        let ctn_l = cur_tab_no - (i-1)
        if ctn_l > 0
            " count all / edited files
            let bufnrs = tabpagebuflist(ctn_l)
            for bn in bufnrs
                let all_wins += 1
                if (match(l_opened_bn, bn) == -1) && (getbufvar(bn, '&buftype') == '')
                    let all_files += 1
                    call add(l_opened_bn, bn)
                    if getbufvar(bn, '&modified')
                        let is_edit += 1
                    endif
                endif
            endfor
            " add title to tabline if possible
            if tab_fin_l == 0
                let title = s:get_title(ctn_l)
                if tab_len+len(title)+1 < width || ctn_l == cur_tab_no
                    let tab_len += len(title)+1
                    let tmp_s = '%'.ctn_l.'T'
                    let tmp_s .= '%#' . (ctn_l == cur_tab_no ? 'TabLineSel' : 'TabLine') . '#'
                    let tmp_s .= title
                    let tmp_s .= '%#TabLineFill# '
                    let s = tmp_s.s
                    " set header & footer
                    if ctn_l == 1
                        let header = ''
                    endif
                    if ctn_l == tabpagenr('$')
                        let footer = ''
                    endif
                else
                    " finish to add the left side.
                    let tab_fin_l = 1
                endif
            endif
        endif

        " right side if current tab page.
        let ctn_r = cur_tab_no + i
        if ctn_r <= tabpagenr('$')
            " count all / edited files
            let bufnrs = tabpagebuflist(ctn_r)
            for bn in bufnrs
                let all_wins += 1
                if (match(l_opened_bn, bn) == -1) && (getbufvar(bn, '&buftype') == '')
                    let all_files += 1
                    call add(l_opened_bn, bn)
                    if getbufvar(bn, '&modified')
                        let is_edit += 1
                    endif
                endif
            endfor
            " add title to tabline if possible
            if tab_fin_r == 0
                let title = s:get_title(ctn_r)
                if tab_len+len(title)+1 < width
                    let tab_len += len(title)+1
                    let tmp_s = '%'.ctn_r.'T'
                    let tmp_s .= '%#TabLine#'
                    let tmp_s .= title
                    let tmp_s .= '%#TabLineFill# '
                    let s .= tmp_s
                    " set footer
                    if ctn_r == tabpagenr('$')
                        let footer = ''
                    endif
                else
                    " finish to add the right side.
                    let tab_fin_r = 1
                endif
            endif
        endif
    endfor
    let header = '_'.is_edit.'/'.all_files.'('.all_wins.')_ ' . header
    " color setting
    let header = '%#TabLineFill#'.header.'%#TabLineFill#'
    " color setting
    let footer = '%#TabLineFill#'.footer.'%#TabLineFill#'
    " 右寄せしてディレクトリ表示
    let rtabline = '%=%#TabLineDir#'.cdir.'%#TabLineFill#'
    let s = header.s.footer.rtabline
    return s
endfunction

let &tabline = '%!'. s:SID_PREFIX() . 'my_tabline()'
set showtabline=2 " 常にタブラインを表示
" }}}

"開いているファイル情報を表示（ざっくり）{{{
if has('win32')
    let s:ls = 'dir '
else
    let s:ls='ls -l '
endif
command! FileInfo execute "!" . s:ls . expand('%')
"}}}

" 新しいtabで関数ジャンプする {{{
function! <SID>Tab_Jump()
    let l:ctabnr = tabpagenr()
    let l:cword = expand('<cword>')
    silent cd %:h
    tabnew
    " tag listを初期化
    try
        $pop
    catch
    endtry
    execute "tjump " . l:cword
    if expand("%:t") == ""
        quit
        execute "normal! " . l:ctabnr . "gt"
    endif
    silent cd -
endfunction

"jump先をnew tabで開く
nnoremap <silent> <c-j> :call <SID>Tab_Jump()<CR>

" }}}

"clipboardを新しいwindowに保存{{{
function! Show_clipboard()
    """ open ... 1 open 0 ... get back
    execute( winnr('$') . 'wincmd l')
    if &filetype != 'clipboard'
        silent botright vertical 20split ClipBoard
        setlocal noreadonly
        setlocal number
        setlocal noswapfile
        setlocal nobackup
        setlocal noundofile
        setlocal filetype=clipboard
        setlocal buftype=nofile
        setlocal nobuflisted
        setlocal wrap
        setlocal report=9999
    endif
    put *
    wincmd p

endfunction
nnoremap <Leader>p :call Show_clipboard()<CR>
"}}}

" 関数じゃないけどInsertを抜けるときに日本語入力をoff {{{
if !exists('$SSH_CONNECTION') && exists('g:l_auto_ime_off')   " ※ssh先ではhostのを変えるので意味なし
    if has('win32')
        autocmd local InsertLeave * set iminsert=0
    elseif has('mac')
            " 参考：https://rcmdnk.com/blog/2017/03/10/computer-mac-vim/
        if has('nvim')
            " nvimはjob_startが無い？っぽいのでとりあえず昔の方法で
            let g:l_imeoff = 'osascript -e "tell application \"System Events\" to key code 102"'
            autocmd local InsertLeave * :call system(g:l_imeoff)
        else
            " 参考2: https://moyapro.com/2019/04/14/disable-ime-on-mac-vim/
            " 非同期で動かせるらしい
            autocmd local InsertLeave * call job_start(
                        \ ['osascript', '-e', 'tell application "System Events" to key code {102}'],
                        \ #{in_io: 'null', out_io: 'null', err_io: 'null'})
        endif
    endif
    set ttimeoutlen=1
endif

" }}}

"" active window {{{

"let g:l_cur_winnr = 1
"autocmd local WinEnter let g:l_cur_winnr = winnr()
autocmd local WinEnter * if &buftype != 'quickfix' | set statusline=%!Set_statusline(1) | endif
autocmd local WinLeave * if &buftype != 'quickfix' | setlocal statusline=%!Set_statusline(0,g:l_statusline_off) | endif

""}}}

" conflict commentを検索
command! SearchConf /<<<<<<<\|=======\|>>>>>>>

" expandを打つのがめんどくさい
command! -nargs=1 Echopand echo expand(<f-args>)

" ipython を呼ぶ用
" {{{
command! Ipython2 botright terminal ipython2
command! Ipython3 botright terminal ipython3
if exists('&pyxversion') && &pyxversion == 2
    command! Ipython Ipython2
else
    command! Ipython Ipython3
endif
" }}}

" self-made plugin/functionファイル読み込み
let s:plugin_file = g:vimdir . "rcdir/vimrc_plugin.vim"
if filereadable(s:plugin_file)
    execute "source " . s:plugin_file
endif

" nnoremap <Leader><Leader> :map mapleader<CR>
function! <SID>leader_map()
    map <Leader>
endfunction
nnoremap <silent> <Leader><Leader> :call <SID>leader_map()<CR>
" }}}

""##########file, directory関連 "{{{
autocmd local BufRead * let b:cfile = expand("%:p")
autocmd local BufRead * let b:cdir = expand("%:p:h")

let g:l_last_file = {'win':'', 'tab':''}
autocmd local WinLeave * let g:l_last_file['win'] = expand("%:p")
autocmd local TabClosed * let g:l_last_file['tab'] = g:l_last_file['win']
command! LastTab execute "tabnew " . g:l_last_file['tab']
command! LastWin execute "vsplit " . g:l_last_file['win']

"検索pathを追加
set path+=../include
if exists("$LD_LIBRARY_PATH")
    set path+=$LD_LIBRARY_PATH
endif

" test用directoryを追加
let s:test_vim_dir = g:vimdir . 'test'
if !isdirectory(s:test_vim_dir)
    call mkdir(s:test_vim_dir)
endif
execute 'set runtimepath^=' . s:test_vim_dir
"}}}

""##########ファイル読み込み "{{{
"dein directoryがあれば読み込み
let s:dein_file = g:vimdir . "rcdir/vimrc_dein.vim"
if isdirectory(g:vimdir . 'dein') && filereadable(s:dein_file)
    execute "source " . s:dein_file
endif

"カラーの変更
let s:color_file = g:vimdir . "rcdir/vimrc_color.vim"
if !has('gui_running')
    if filereadable(s:color_file) && (&t_Co == 256)
        execute "source " . s:color_file
    endif
endif

"local設定ファイル (2nd)
let s:localrc = g:vimdir . 'rcdir/vimrc.mine'
if filereadable(s:localrc)
    execute "source " . s:localrc
endif
"}}}

